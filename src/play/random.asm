; Top-hole Golf
; Copyright 2020-2021 Matthew Clarke


rand_c_BEGIN = *

; *****************
; *** CONSTANTS ***
; *****************
rand_c_MIX_LIMIT = 64


; *****************
; *** VARIABLES ***
; *****************
rand_v_seq !byte 209,133,204,21,227,25,56,214,36,8,147,139,89,240,180,54,151,176,244,85,38,109,243,168,92,82,206,130,217,121,171,192,166,5,65,201,253,156,200,129,136,160,86,12,238,39,27,213,47,202,224,215,172,165,233,72,101,77,167,104,112,245,232,207,114,158,24,107,64,106,188,132,231,229,78,210,99,91,218,216,100,11,145,45,40,173,184,186,61,41,221,177,143,194,98,103,87,252,22,161,74,110,95,212,51,97,236,93,88,60,71,169,108,222,17,219,105,157,4,237,34,155,96,205,197,248,162,73,163,223,241,211,84,198,113,235,128,42,2,75,249,153,53,13,18,170,62,152,203,58,29,69,44,116,3,225,239,254,67,37,246,175,79,146,43,117,10,187,149,68,90,242,55,23,195,250,50,251,35,142,127,228,124,59,102,80,7,234,0,115,220,199,185,49,126,81,179,141,174,16,154,183,226,190,137,134,140,118,148,131,111,52,6,63,20,150,26,14,9,189,181,144,76,123,138,182,247,230,28,208,57,32,164,19,94,178,125,255,70,119,33,191,120,48,66,15,30,83,46,159,196,1,193,122,135,31
rand_v_iter !byte   0


; *******************
; ****** MACROS *****
; *******************


; *******************
; *** SUBROUTINES ***
; *******************
!zone {
rand_s_reset
    +clr rand_v_iter
    jsr rand_s_shuffle
    rts
; end sub rand_s_reset
} ; !zone

; **************************************************

!zone {
.ITER           = MATHS0

rand_s_shuffle
    ldx #0
.loop
    stx .ITER

    lda rand_v_seq,x
    sta MATHS1
    tay

    txa
    asl
    tax
    lda rand_v_seq,x
    eor MATHS1
    tax

    lda rand_v_seq,x
    pha
    lda rand_v_seq,y
    sta rand_v_seq,x
    pla
    sta rand_v_seq,y

    ldx .ITER
    inx
    cpx #rand_c_MIX_LIMIT
    bne .loop

    rts
; end sub rand_s_shuffle
} ; !zone

; **************************************************

; OUTPUT:   A = (pseudo-) random 8-bit number.
!zone {
rand_s_get
    ldx rand_v_iter    
    lda rand_v_seq,x
    eor shared_v_random_seed
    inc rand_v_iter
    bne .end
    pha
    ; FIXME: can't allow this to happen while a shot is in progress!
    lda ball_v_current_state
    bne +
    jsr rand_s_shuffle
+
    pla
.end
    rts
; end sub rand_s_get
} ; !zone

; **************************************************

; Same as rand_s_get but will never re-shuffle.
; NOTE: preserves value of X.
!zone {
rand_s_get_fast
    stx RANDOM_X_TMP

    ldx rand_v_iter    
    lda rand_v_seq,x
    eor shared_v_random_seed
    inc rand_v_iter

    ldx RANDOM_X_TMP
    rts
; end sub rand_s_get_fast
} ; !zone

; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************
; **************************************************

rand_c_SIZE = *-rand_c_BEGIN

